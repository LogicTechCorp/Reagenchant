buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url = "https://repo.spongepowered.org/repository/maven-public/" }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
    }
}

plugins {
    id 'org.cadixdev.licenser' version '0.5.0'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

version = "${minecraft_version}-${mod_version}"
group = 'com.github.logictechcorp'
archivesBaseName = "${mod_id}"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

license {
    header = project.file('LICENSE-header.txt')
    sourceSets = [project.sourceSets.main]
    include '**/reagenchant/**/*.java'
    ext {
        year = new Date().format('YYYY')
    }
}

mixin {
    add sourceSets.main, "${mod_id}.refmap.json"
}

minecraft {
    mappings channel: 'snapshot', version: "${mappings_version}"  
    runs {
        client {
            workingDirectory project.file('run')
            property 'mixin.env.disableRefMap', "true"
            arg "-mixin.config=${mod_id}.mixins.json".toString()
            mods {
                Reagenchant {
                    source sourceSets.main
                }
            }
        }
        server {
            workingDirectory project.file('run')
            property 'mixin.env.disableRefMap', "true"
            arg "-mixin.config=${mod_id}.mixins.json".toString()
            mods {
                Reagenchant {
                    source sourceSets.main
                }
            }
        }
        data {
            workingDirectory project.file('run')
            args '--mod', "${mod_id}".toString(), '--all', '--output', file('src/data_gen/resources/')
            mods {
                Reagenchant {
                    source sourceSets.main
                }
            }
        }
    }
}

repositories {
    maven {
        url "https://maven.jaackson.me"
    }
    maven {
        url "https://dvs1.progwml6.com/files/maven/"
    }
    maven {
        url "https://modmaven.k-4u.nl"
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    implementation fg.deobf("com.minecraftabnormals:abnormals-core:${minecraft_version}-${abnormals_core_version}")
    compileOnly fg.deobf("mezz.jei:jei-${minecraft_version}:${jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-${minecraft_version}:${jei_version}")
}

jar {
    manifest {
        attributes([
            "Specification-Title"     : "${mod_name}",
            "Specification-Vendor"    : 'LogicTechCorp',
            "Specification-Version"   : '1',
            "Implementation-Title"    : "${mod_name}",
            "Implementation-Version"  : "${mod_version}",
            "Implementation-Vendor"   : 'LogicTechCorp',
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConfigs"            : "${mod_id}.mixins.json"
        ])
    }
}

build {
    dependsOn(updateLicenses)
}